// Adapted from https://pest.rs/book/examples/json.html
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
ident      = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "-")* }
parameters =  { value ~ ("," ~ WHITESPACE? ~ value)* }
fn_call    = ${ ident ~ "(" ~ parameters? ~ ")" }
object     =  {
    "{" ~ "}"
  | "{" ~ pair ~ ("," ~ pair)* ~ "}"
}
pair       =  { string ~ WHITESPACE? ~ ":" ~ WHITESPACE? ~ value }
array      =  {
    "[" ~ "]"
  | "[" ~ value ~ ("," ~ value)* ~ "]"
}
string     = ${ "\"" ~ inner ~ "\"" }
inner      = @{ char* }
char       =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
boolean    =  { "true" | "false" }
null       =  { "null" }
number     = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
value      = _{ fn_call | boolean | ident | string | object | array | number | null }
expr       = _{ SOI ~ value ~ EOI }
